group 'dk.sdu.cloud'
version '0.5.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.0'
    ext.ktor_version = '0.9.1-alpha-8'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }

    maven {
        url("https://cloud.sdu.dk/archiva/repository/internal")
        credentials {
            username(eScienceCloudUser)
            password(eScienceCloudPassword)
        }
    }
    maven {
        url("https://cloud.sdu.dk/archiva/repository/snapshots")
        credentials {
            username(eScienceCloudUser)
            password(eScienceCloudPassword)
        }
    }
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    /*
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.20'
    */

    // Kafka
    compile group: 'org.apache.kafka', name: 'kafka-clients', version: '1.0.0'
    compile group: 'org.apache.kafka', name: 'kafka-streams', version: '1.0.0'

    // Serialization
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0"

    // Client dependencies (needed to export definitions to clients)
    compile "dk.sdu.cloud:client-core:0.1.0-SNAPSHOT"

    // ktor
    compile group: 'io.ktor', name: 'ktor-server-core', version: "$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"

    // Service discovery
    compile(group: "org.apache.zookeeper", name: "zookeeper", version: "3.4.11") {
        exclude(group: "org.slf4j", module: "slf4j-log4j12")
    }
    compile group: "com.github.zafarkhaja", name: "java-semver", version: "0.9.0"

    // JWTs
    compile "com.auth0:java-jwt:3.0.1"

    // Testing
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }

    // For this to work you should create a file at ~/.gradle/gradle.properties
    // This file should contain the configuration for the repository, user, and password.
    //
    // Example:
    // eScienceCloudRepository=http://localhost:18080/repository/internal
    // eScienceCloudUser=admin
    // eScienceCloudPassword=qwer
    //
    repositories {
        maven {
            def resolvedUrl = "https://cloud.sdu.dk/archiva/repository/"
            if (project.version.endsWith("-SNAPSHOT")) resolvedUrl += "snapshots"
            else resolvedUrl += "internal"
            url(resolvedUrl)

            credentials {
                username(eScienceCloudUser)
                password(eScienceCloudPassword)
            }
        }
    }
}

