buildscript {
    ext.kotlin_version = '1.2.61'
    ext.dokka_version = "0.9.16"

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC9.2")
}

detekt {
    toolVersion = "1.0.0.RC9.2"
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("../shared-gradle/detekt.yml")
}

group 'dk.sdu.cloud'
version '1.0.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'


dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
}
repositories {
    mavenCentral()
    maven { url("https://kotlin.bintray.com/ktor") }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }

    def username = System.getenv("ESCIENCE_MVN_USER") ?: eScienceCloudUser
    def password = System.getenv("ESCIENCE_MVN_PASSWORD") ?: eScienceCloudPassword

    maven {
        url("https://cloud.sdu.dk/archiva/repository/internal")
        credentials {
            it.username(username)
            it.password(password)
        }
    }
    maven {
        url("https://cloud.sdu.dk/archiva/repository/snapshots")
        credentials {
            it.username(username)
            it.password(password)
        }
    }

}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.4.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.2'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'

    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.9.1"
    compile group: 'org.kamranzafar', name: 'jtar', version: '2.3'

    testCompile "junit:junit:4.12"
    testCompile "io.mockk:mockk:1.8"
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jacocoTestReport {
    reports {
        xml {
            enabled true // coveralls plugin depends on xml format report
        }

        html {
            enabled true
        }
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

kotlin {
    experimental {
        coroutines "enable"
    }
}

apply plugin: 'application'
mainClassName = "dk.sdu.cloud.sync.MainKt"

